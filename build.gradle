import org.springframework.boot.gradle.tasks.run.BootRun

buildscript {
    ext {
        springBootVersion = '2.2.4.RELEASE'
    }
    repositories {
        mavenCentral()
        jcenter()

        maven { url "https://plugins.gradle.org/m2/" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("net.serenity-bdd:serenity-gradle-plugin:2.1.6")
    }
}

plugins {
    id 'application'
    id 'checkstyle'
    id 'com.github.ben-manes.versions' version '0.27.0'
    id 'com.jfrog.bintray' version '1.8.4'
    id 'info.solidsoft.pitest' version '1.4.7'
    id 'io.spring.dependency-management' version '1.0.9.RELEASE'
    id 'jacoco'
    id 'org.owasp.dependencycheck' version '5.3.0'
    id 'org.sonarqube' version '2.8'
    id 'org.springframework.boot' version '2.2.4.RELEASE'
}

apply plugin: 'net.serenity-bdd.aggregator'

group = 'uk.gov.hmcts.reform.divorce'
version = '1.0.0'
sourceCompatibility = 1.8
targetCompatibility = 1.8

jar {
    manifest {
        attributes 'Implementation-Title': project.name,
            'Implementation-Version': project.version
    }
    archiveName = 'div-case-maintenance-service.jar'
}

bootJar {
    archiveName = 'div-case-maintenance-service.jar'
    manifest {
        attributes 'Implementation-Title': project.name,
            'Implementation-Version': project.version
    }
}

mainClassName = 'uk.gov.hmcts.reform.divorce.casemaintenanceservice.CaseMaintenanceServiceApplication'

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()

    maven { url 'https://repo.spring.io/libs-milestone' }
    maven { url "https://dl.bintray.com/hmcts/hmcts-maven" }
    maven { url "http://repo.maven.apache.org/maven2" }
}

distributions {
    main {
        contents {
            from(file("$projectDir/lib/AI-Agent.xml")) {
                into "lib"
            }
        }
    }
}

checkstyle.toolVersion = '8.29'
checkstyle.configFile = new File(rootDir, "checkstyle.xml")

// make build fail on Checkstyle issues (https://github.com/gradle/gradle/issues/881)
tasks.withType(Checkstyle).each { checkstyleTask ->
    checkstyleTask.doLast {
        reports.all { report ->
            def outputFile = report.destination
            if (outputFile.exists() && outputFile.text.contains("<error ")) {
                throw new GradleException("There were checkstyle warnings! For more info check $outputFile")
            }
        }
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
    suppressionFile = 'dependency-check-suppressions.xml'
}

def versions = [
    bcpkixJdk15on: '1.64',
    ccdStoreClient: '4.6.4',
    commonsBeanUtils: '1.9.4',
    commonsIo: '2.6',
    commonsLang3: '3.7',
    feignHttpClient: '10.2.0',
    gradlePitest: '1.4.5',
    guava: '28.1-jre',
    idamClient: '1.1.0',
    jackson: '2.10.0',
    jsonAssert: '1.2.3',
    junit: '4.12',
    lombok: '1.16.16',
    pitest: '1.4.10',
    puppyCrawl: '8.29',
    reformHealth: '0.0.5',
    reformPropertiesVolume: '0.0.4',
    reformsJavaLogging: '5.1.1',
    reformSpringBootAutoConfigure: '2.0.0',
    restAssured: '3.0.3',
    serenity: '2.0.81',
    serenityCucumber: '1.9.46',
    serviceTokenGenerator: '3.0.0',
    sonarPitest: '0.5',
    springCloudDependencies: '2.2.1.RELEASE',
    springfoxSwagger: '2.9.2',
    springSecurityCrypto: '5.2.0.RELEASE',
    springSecurityRsa: '1.0.8.RELEASE',
    tomcat: '9.0.30',
    unirest: '1.4.9',
    wiremockVersion: '2.8.0'
]

dependencies {
    compile('org.springframework.boot:spring-boot-starter-actuator')
    compile('org.springframework.boot:spring-boot-starter-web')
    compile('org.springframework.boot:spring-boot-starter-aop')

    compileOnly("org.projectlombok:lombok:${versions.lombok}")
    testCompileOnly("org.projectlombok:lombok:${versions.lombok}")
    annotationProcessor("org.projectlombok:lombok:${versions.lombok}")
    testAnnotationProcessor("org.projectlombok:lombok:${versions.lombok}")
    integrationTestCompile group: 'org.projectlombok', name: 'lombok', version: versions.lombok

    compile group: 'org.springframework', name: 'spring-context-support'
    compile (group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign')
        {
            exclude group: 'io.reactivex', module: 'io.reactivex'
            exclude group: 'io.reactivex', module: 'rxnetty'
            exclude group: 'io.reactivex', module: 'rxnetty-contexts'
            exclude group: 'io.reactivex', module: 'rxnetty-servo'
        }

    compile group: 'io.github.openfeign', name: 'feign-httpclient', version: versions.feignHttpClient
    compile group: 'uk.gov.hmcts.reform', name: 'http-proxy-spring-boot-autoconfigure', version: versions.reformSpringBootAutoConfigure
    compile (group: 'com.fasterxml.jackson.core', name:'jackson-databind', version: versions.jackson) {
        force = true
    }

    compile group: 'org.springframework.boot', name: 'spring-boot-starter-json'
    compile group: 'com.puppycrawl.tools', name: 'checkstyle', version:  versions.puppyCrawl
    compile group: 'commons-io', name: 'commons-io', version: versions.commonsIo
    compile group: 'io.springfox', name: 'springfox-swagger2', version: versions.springfoxSwagger
    compile group: 'io.springfox', name: 'springfox-swagger-ui', version: versions.springfoxSwagger
    compile group: 'org.apache.commons', name: 'commons-lang3', version : versions.commonsLang3
    compile group: 'uk.gov.hmcts.reform', name: 'health-spring-boot-starter', version: versions.reformHealth
    compile group: 'uk.gov.hmcts.reform', name: 'properties-volume-spring-boot-starter', version: versions.reformPropertiesVolume
    compile group: 'uk.gov.hmcts.reform', name: 'logging', version: versions.reformsJavaLogging
    compile group: 'uk.gov.hmcts.reform', name: 'logging-appinsights', version: versions.reformsJavaLogging
    compile group: 'uk.gov.hmcts.reform', name: 'logging-spring', version: versions.reformsJavaLogging
    compile group: 'uk.gov.hmcts.reform', name: 'logging-httpcomponents', version: versions.reformsJavaLogging
    compile group: 'uk.gov.hmcts.reform', name: 'idam-client', version: versions.idamClient
    compile (group: 'uk.gov.hmcts.reform', name:'service-auth-provider-client', version: versions.serviceTokenGenerator)
        {
            exclude group: 'io.reactivex', module: 'io.reactivex'
            exclude group: 'io.reactivex', module: 'rxnetty'
            exclude group: 'io.reactivex', module: 'rxnetty-contexts'
            exclude group: 'io.reactivex', module: 'rxnetty-servo'
        }

    compile (group: 'uk.gov.hmcts.reform', name: 'core-case-data-store-client', version: versions.ccdStoreClient)
        {
            exclude group: 'io.reactivex', module: 'io.reactivex'
            exclude group: 'io.reactivex', module: 'rxnetty'
            exclude group: 'io.reactivex', module: 'rxnetty-contexts'
            exclude group: 'io.reactivex', module: 'rxnetty-servo'
        }

    compile (group: 'org.apache.tomcat.embed', name:'tomcat-embed-core', version: versions.tomcat) {
        force = true
    }
    compile (group: 'org.apache.tomcat.embed', name:'tomcat-embed-websocket', version: versions.tomcat) {
        force = true
    }
    compile (group: 'org.apache.tomcat.embed', name:'tomcat-embed-el', version: versions.tomcat) {
        force = true
    }

    compile (group: 'com.google.guava', name:'guava', version: versions.guava) {
        force = true
    }

    compile (group: 'org.bouncycastle', name:'bcpkix-jdk15on', version: versions.bcpkixJdk15on) {
        force = true
    }

    compile (group: 'org.springframework.security', name:'spring-security-rsa', version: versions.springSecurityRsa) {
        force = true
    }

    compile (group: 'org.springframework.security', name:'spring-security-crypto', version: versions.springSecurityCrypto) {
        force = true
    }

    compile group: 'org.pitest', name: 'pitest', version: versions.pitest
    compile group: 'info.solidsoft.gradle.pitest', name: 'gradle-pitest-plugin', version: versions.gradlePitest
    compile group: 'org.codehaus.sonar-plugins', name:'sonar-pitest-plugin', version: versions.sonarPitest

    testCompile group: 'com.github.tomakehurst', name:'wiremock', version: versions.wiremockVersion
    testCompile group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock', version: versions.springCloudDependencies
    testCompile('org.springframework.boot:spring-boot-starter-test')
    runtime('org.springframework.boot:spring-boot-devtools')

    //integration test
    integrationTestCompile group: 'com.mashape.unirest', name: 'unirest-java', version: versions.unirest
    integrationTestCompile group: 'io.rest-assured', name: 'rest-assured', version: versions.restAssured
    integrationTestCompile group: 'junit', name: 'junit', version: versions.junit
    integrationTestCompile group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity
    integrationTestCompile group: 'net.serenity-bdd', name: 'serenity-cucumber', version: versions.serenityCucumber
    integrationTestCompile group: 'net.serenity-bdd', name: 'serenity-junit', version: versions.serenity
    integrationTestCompile group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity
    integrationTestCompile group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity
    integrationTestCompile group: 'org.apache.commons', name: 'commons-lang3', version: versions.commonsLang3
    integrationTestCompile group: 'org.skyscreamer', name:'jsonassert', version: versions.jsonAssert
    integrationTestCompile group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    integrationTestCompile (group: 'uk.gov.hmcts.reform', name: 'core-case-data-store-client', version: versions.ccdStoreClient)
        {
            exclude group: 'io.reactivex', module: 'io.reactivex'
            exclude group: 'io.reactivex', module: 'rxnetty'
            exclude group: 'io.reactivex', module: 'rxnetty-contexts'
            exclude group: 'io.reactivex', module: 'rxnetty-servo'
        }
    integrationTestCompile (group: 'uk.gov.hmcts.reform', name:'service-auth-provider-client', version: versions.serviceTokenGenerator)
        {
            exclude group: 'io.reactivex', module: 'io.reactivex'
            exclude group: 'io.reactivex', module: 'rxnetty'
            exclude group: 'io.reactivex', module: 'rxnetty-contexts'
            exclude group: 'io.reactivex', module: 'rxnetty-servo'
        }

    integrationTestCompile group: 'org.springframework', name: 'spring-context-support'
    integrationTestCompile (group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign')
        {
            exclude group: 'io.reactivex', module: 'io.reactivex'
            exclude group: 'io.reactivex', module: 'rxnetty'
            exclude group: 'io.reactivex', module: 'rxnetty-contexts'
            exclude group: 'io.reactivex', module: 'rxnetty-servo'
        }
    integrationTestCompile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.10.2'
    integrationTestCompile group: 'io.github.openfeign', name: 'feign-httpclient', version: versions.feignHttpClient
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir('src/integrationTest/java')
        }
        resources {
            srcDir('src/integrationTest/resources')
        }
    }
}

dependencyManagement {
    dependencies {
        dependency group: 'commons-beanutils', name: 'commons-beanutils', version: versions.commonsBeanUtils

        // solves CVE-2014-3488, CVE-2015-2156, CVE-2019-16869
        dependencySet(group: 'io.netty', version: '4.1.45.Final') {
            entry 'netty-buffer'
            entry 'netty-codec'
            entry 'netty-codec-http'
            entry 'netty-common'
            entry 'netty-handler'
            entry 'netty-resolver'
            entry 'netty-transport'
        }

        dependencySet(
            group: 'com.fasterxml.jackson.core',
            version: versions.jackson
        ) {
            entry 'jackson-core'
            entry 'jackson-databind'
        }
    }
}

dependencies {
    integrationTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    integrationTestCompile(sourceSets.test.output)
}

task smoke(type: Test, description: 'Runs the smoke tests.', group: 'Verification') {
    useJUnit {
        includeCategories 'uk.gov.hmcts.reform.divorce.casemaintenanceservice.category.SmokeTest'
    }
}

task bootRunAat(type: BootRun, description: 'Runs the app using AAT config', dependsOn: 'classes', group: 'Application') {
    environment "http_proxy", "http://proxyout.reform.hmcts.net:8080"
    doFirst() {
        main = bootJar.mainClassName
        classpath = sourceSets.main.runtimeClasspath
        systemProperty 'spring.profiles.active', 'aat'
    }
}

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {
    printf ("Available processors:::::::::: " + Runtime.runtime.availableProcessors().intdiv(2).toString())

    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    forkEvery = 10
    setTestClassesDirs(sourceSets.integrationTest.output.classesDirs)
    setClasspath(sourceSets.integrationTest.runtimeClasspath)

    finalizedBy aggregate
}

pitest {
    targetClasses = ['uk.gov.hmcts.reform.divorce.casemaintenanceservice.controller.*',
                     'uk.gov.hmcts.reform.divorce.casemaintenanceservice.controller.support.*',
                     'uk.gov.hmcts.reform.divorce.casemaintenanceservice.draftstore.factory.*',
                     'uk.gov.hmcts.reform.divorce.casemaintenanceservice.event.ccd.submission.*',
                     'uk.gov.hmcts.reform.divorce.casemaintenanceservice.event.config',
                     'uk.gov.hmcts.reform.divorce.casemaintenanceservice.management.monitoring.health.*',
                     'uk.gov.hmcts.reform.divorce.casemaintenanceservice.service.*',
                     'uk.gov.hmcts.reform.divorce.casemaintenanceservice.util.*']
    excludedClasses = [ ]
    threads = 10
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
    mutationThreshold = 80
}

def sonarExclusions = [
    '**uk/gov/hmcts/reform/divorce/casemaintenanceservice/domain/**/*',
    '**uk/gov/hmcts/reform/divorce/casemaintenanceservice/**/model/**/*',
    '**uk/gov/hmcts/reform/divorce/casemaintenanceservice/**/config/**/*',
    '**uk/gov/hmcts/reform/divorce/casemaintenanceservice/CaseMaintenanceServiceApplication.java',
    '**uk/gov/hmcts/reform/divorce/casemaintenanceservice/client/DraftStoreClient.java'
]

sonarqube {
    properties {
        property "sonar.projectKey", "DivorceCaseMaintenanceService"
        property "sonar.projectName", "Divorce :: case-maintenance-service"
        property "sonar.exclusions", sonarExclusions.join(", ")
        property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.destination.path}"
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    }
}

task developAddRelaseSuffix() {
    version = "${version}-SNAPSHOT"
}

jacocoTestReport {
    executionData(test)

    reports {
        xml.enabled = true
        html.enabled = true
        xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.9
            }
        }
    }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

def debug = System.getProperty("debug")
run {
    if (debug == 'true') {
        jvmArgs = ['-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5010']
    }
}

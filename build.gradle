import org.springframework.boot.gradle.tasks.run.BootRun

buildscript {
    ext {
        springBootVersion = '2.7.14'
        springCloudBomVersion = "2021.0.8"
    }
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url "https://jitpack.io" }
    }
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath("net.serenity-bdd:serenity-gradle-plugin:2.5.7")
    }
}

plugins {
    id 'application'
    id 'checkstyle'
    id 'com.github.ben-manes.versions' version '0.47.0'
    id 'com.jfrog.bintray' version '1.8.5'
    id 'info.solidsoft.pitest' version '1.9.11'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'jacoco'
    id 'org.owasp.dependencycheck' version '8.0.2'
    id 'org.sonarqube' version '4.2.0.3129'
    id 'org.springframework.boot' version '2.7.14'
    id 'au.com.dius.pact' version '4.2.3'
    id 'uk.gov.hmcts.java' version '0.12.27'
}

apply plugin: 'checkstyle'
apply plugin: 'com.github.ben-manes.versions'
apply plugin: 'io.spring.dependency-management'
apply plugin: 'jacoco'
apply plugin: 'java'
apply plugin: 'org.owasp.dependencycheck'
apply plugin: 'net.serenity-bdd.aggregator'

group = 'uk.gov.hmcts.reform.divorce'
version = '1.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

bootJar {
    archiveFileName = 'div-case-maintenance-service.jar'
    manifest {
        attributes 'Implementation-Title': project.name,
            'Implementation-Version': project.version
    }
}

springBoot {
    mainClass = 'uk.gov.hmcts.reform.divorce.casemaintenanceservice.CaseMaintenanceServiceApplication'
}

compileJava {
    options.compilerArgs << '-parameters' << '-Xlint:deprecation'
}

compileTestJava {
    options.compilerArgs << '-Xlint:deprecation'
}

repositories {
    mavenLocal()
    mavenCentral()
    jcenter()
    maven { url 'https://repo.spring.io/libs-milestone' }
    maven { url "https://jitpack.io" }
}

distributions {
    main {
        contents {
            from(file("$projectDir/lib/AI-Agent.xml")) {
                into "lib"
            }
        }
    }
}

checkstyle.toolVersion = '8.32'
checkstyle.configFile = new File(rootDir, "checkstyle.xml")
checkstyle.maxWarnings(0)

// make build fail on Checkstyle issues (https://github.com/gradle/gradle/issues/881)
tasks.withType(Checkstyle).each { checkstyleTask ->
    checkstyleTask.doLast {
        reports.all { report ->
            def outputFile = report.destination
            if (outputFile.exists() && outputFile.text.contains("<error ")) {
                throw new GradleException("There were checkstyle warnings! For more info check $outputFile")
            }
        }
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntime.extendsFrom testRuntime
    contractTestImplementation.extendsFrom testImplementation
    contractTestRuntimeOnly.extendsFrom testRuntime
}

// https://jeremylong.github.io/DependencyCheck/dependency-check-gradle/configuration.html
dependencyCheck {
    // Specifies if the build should be failed if a CVSS score above a specified level is identified.
    // range of 0-10 fails the build, anything greater and it doesn't fail the build
    failBuildOnCVSS = System.getProperty('dependencyCheck.failBuild') == 'true' ? 0 : 11
    suppressionFile = 'dependency-check-suppressions.xml'
    analyzers {
        // Disable scanning of .NET related binaries
        assemblyEnabled = false
    }
}

def versions = [
    bcpkixJdk15on: '1.64',
    ccdStoreClient: '4.7.6',
    commonsBeanUtils: '1.9.4',
    commonsIo: '2.7',
    commonsLang3: '3.7',
    gradlePitest: '1.5.2',
    guava: '32.1.3-jre',
    httpComponents: '4.5.13',
    idamClient: '1.1.0',
    jackson: '2.13.2',
    jsonAssert: '1.2.3',
    junit: '4.13.2',
    junit5: '5.9.3',
    lombok: '1.18.30',
    logbackVersion: '1.2.10',
    pact_version: '4.1.7',
    pitest: '1.5.2',
    puppyCrawl: '8.29',
    reformPropertiesVolume: '0.0.4',
    reformsJavaLogging: '5.1.7',
    restAssured: '4.2.1',
    serenity: '2.5.8',
    serenityCucumber: '1.9.51',
    serviceTokenGenerator: '3.0.0',
    sonarPitest: '0.5',
    springCloudDependencies: '2.2.1.RELEASE',
    springDoc : '1.6.9',
    springSecurityCrypto: '5.4.5',
    springSecurityRsa: '1.0.9.RELEASE',
    tomcat: '9.0.80',
    unirest: '1.4.9',
    wiremockVersion: '2.27.2'
]

tasks.withType(JavaExec).configureEach {
    javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

tasks.withType(Test).configureEach {
    useJUnitPlatform()

    testLogging {
        exceptionFormat = 'full'
    }
}

test {
    useJUnit()
    failFast = true
}

dependencies {

    contractTestImplementation sourceSets.main.runtimeClasspath
    contractTestImplementation sourceSets.test.runtimeClasspath

    implementation('org.springframework.boot:spring-boot-starter-actuator')
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('org.springframework.boot:spring-boot-starter-aop')
    implementation('org.springframework.boot:spring-boot-starter-validation')

    implementation("org.projectlombok:lombok:${versions.lombok}")

    testImplementation("org.projectlombok:lombok:${versions.lombok}")
    testImplementation('com.github.hmcts:fortify-client:1.2.0:all')
    annotationProcessor("org.projectlombok:lombok:${versions.lombok}")
    testAnnotationProcessor("org.projectlombok:lombok:${versions.lombok}")
    integrationTestImplementation group: 'org.projectlombok', name: 'lombok', version: versions.lombok

    implementation group: 'com.sun.xml.bind', name: 'jaxb-osgi', version: '4.0.3'
    implementation group: 'org.springframework', name: 'spring-context-support'
    implementation (group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign')
        {
            exclude group: 'io.reactivex', module: 'io.reactivex'
            exclude group: 'io.reactivex', module: 'rxnetty'
            exclude group: 'io.reactivex', module: 'rxnetty-contexts'
            exclude group: 'io.reactivex', module: 'rxnetty-servo'
        }

    implementation group: 'com.fasterxml.jackson.core', name:'jackson-databind', version: versions.jackson
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: versions.jackson

    implementation group: 'org.springframework.boot', name: 'spring-boot-starter-json'
    implementation group: 'com.puppycrawl.tools', name: 'checkstyle', version:  versions.puppyCrawl
    implementation group: 'commons-io', name: 'commons-io', version: versions.commonsIo
    implementation group: 'org.springdoc', name: 'springdoc-openapi-ui', version: versions.springDoc
    implementation group: 'io.swagger.core.v3', name: 'swagger-annotations', version: '2.2.17'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version : versions.commonsLang3
    implementation group: 'uk.gov.hmcts.reform', name: 'properties-volume-spring-boot-starter', version: versions.reformPropertiesVolume

    implementation group: 'ch.qos.logback', name: 'logback-classic', version: versions.logbackVersion
    implementation group: 'ch.qos.logback', name: 'logback-core', version: versions.logbackVersion

    implementation group: 'org.apache.httpcomponents', name: 'httpclient', version: versions.httpComponents
    implementation group: 'org.apache.httpcomponents', name: 'fluent-hc', version: versions.httpComponents

    implementation group: 'uk.gov.hmcts.reform', name: 'logging', version: versions.reformsJavaLogging
    implementation group: 'uk.gov.hmcts.reform', name: 'logging-appinsights', version: versions.reformsJavaLogging
    implementation group: 'uk.gov.hmcts.reform', name: 'logging-spring', version: versions.reformsJavaLogging
    implementation (group: 'uk.gov.hmcts.reform', name: 'logging-httpcomponents', version: versions.reformsJavaLogging){
        exclude group: 'org.apache.httpcomponents', 'module': 'httpclient'
    }
    implementation group: 'uk.gov.hmcts.reform', name: 'idam-client', version: versions.idamClient
    implementation (group: 'uk.gov.hmcts.reform', name:'service-auth-provider-client', version: versions.serviceTokenGenerator)
        {
            exclude group: 'io.reactivex', module: 'io.reactivex'
            exclude group: 'io.reactivex', module: 'rxnetty'
            exclude group: 'io.reactivex', module: 'rxnetty-contexts'
            exclude group: 'io.reactivex', module: 'rxnetty-servo'
        }

    implementation (group: 'uk.gov.hmcts.reform', name: 'core-case-data-store-client', version: versions.ccdStoreClient)
        {
            exclude group: 'io.reactivex', module: 'io.reactivex'
            exclude group: 'io.reactivex', module: 'rxnetty'
            exclude group: 'io.reactivex', module: 'rxnetty-contexts'
            exclude group: 'io.reactivex', module: 'rxnetty-servo'
        }

    implementation (group: 'org.apache.tomcat.embed', name:'tomcat-embed-core', version: versions.tomcat) {
        force = true
    }
    implementation (group: 'org.apache.tomcat.embed', name:'tomcat-embed-websocket', version: versions.tomcat) {
        force = true
    }
    implementation (group: 'org.apache.tomcat.embed', name:'tomcat-embed-el', version: versions.tomcat) {
        force = true
    }
    implementation (group: 'com.google.guava', name:'guava', version: versions.guava) {
        force = true
    }
    implementation (group: 'org.bouncycastle', name:'bcpkix-jdk15on', version: versions.bcpkixJdk15on) {
        force = true
    }
    implementation (group: 'org.springframework.security', name:'spring-security-rsa', version: versions.springSecurityRsa) {
        force = true
    }
    implementation (group: 'org.springframework.security', name:'spring-security-crypto', version: versions.springSecurityCrypto) {
        force = true
    }

    implementation group: 'org.pitest', name: 'pitest', version: versions.pitest
    implementation group: 'info.solidsoft.gradle.pitest', name: 'gradle-pitest-plugin', version: versions.gradlePitest
    implementation group: 'org.codehaus.sonar-plugins', name:'sonar-pitest-plugin', version: versions.sonarPitest

    testImplementation group: 'com.github.tomakehurst', name:'wiremock', version: versions.wiremockVersion
    testImplementation group: 'org.springframework.cloud', name: 'spring-cloud-contract-wiremock', version: versions.springCloudDependencies
    testImplementation('org.springframework.boot:spring-boot-starter-test')
    runtimeOnly('org.springframework.boot:spring-boot-devtools')

    //integration test
    integrationTestImplementation group: 'com.mashape.unirest', name: 'unirest-java', version: versions.unirest
    integrationTestImplementation group: 'junit', name: 'junit', version: versions.junit
    integrationTestImplementation group: 'net.serenity-bdd', name: 'serenity-core', version: versions.serenity
    integrationTestImplementation group: 'net.serenity-bdd', name: 'serenity-cucumber', version: versions.serenityCucumber
    integrationTestImplementation group: 'net.serenity-bdd', name: 'serenity-junit', version: versions.serenity
    integrationTestImplementation group: 'net.serenity-bdd', name: 'serenity-rest-assured', version: versions.serenity
    integrationTestImplementation group: 'net.serenity-bdd', name: 'serenity-spring', version: versions.serenity
    integrationTestImplementation group: 'io.rest-assured', name: 'rest-assured', version: versions.restAssured
    integrationTestImplementation group: 'io.rest-assured', name: 'rest-assured-all', version: versions.restAssured
    integrationTestImplementation group: 'org.apache.commons', name: 'commons-lang3', version: versions.commonsLang3
    integrationTestImplementation group: 'org.skyscreamer', name:'jsonassert', version: versions.jsonAssert
    integrationTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    integrationTestImplementation (group: 'uk.gov.hmcts.reform', name: 'core-case-data-store-client', version: versions.ccdStoreClient)
        {
            exclude group: 'io.reactivex', module: 'io.reactivex'
            exclude group: 'io.reactivex', module: 'rxnetty'
            exclude group: 'io.reactivex', module: 'rxnetty-contexts'
            exclude group: 'io.reactivex', module: 'rxnetty-servo'
        }
    integrationTestImplementation (group: 'uk.gov.hmcts.reform', name:'service-auth-provider-client', version: versions.serviceTokenGenerator)
        {
            exclude group: 'io.reactivex', module: 'io.reactivex'
            exclude group: 'io.reactivex', module: 'rxnetty'
            exclude group: 'io.reactivex', module: 'rxnetty-contexts'
            exclude group: 'io.reactivex', module: 'rxnetty-servo'
        }

    integrationTestImplementation group: 'org.springframework', name: 'spring-context-support'
    integrationTestImplementation (group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign')
        {
            exclude group: 'io.reactivex', module: 'io.reactivex'
            exclude group: 'io.reactivex', module: 'rxnetty'
            exclude group: 'io.reactivex', module: 'rxnetty-contexts'
            exclude group: 'io.reactivex', module: 'rxnetty-servo'
        }
    integrationTestImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.12.0'

    contractTestImplementation group: 'org.springframework', name: 'spring-context-support'
    contractTestImplementation (group: 'org.springframework.cloud', name: 'spring-cloud-starter-openfeign')
            {
                exclude group: 'io.reactivex', module: 'io.reactivex'
                exclude group: 'io.reactivex', module: 'rxnetty'
                exclude group: 'io.reactivex', module: 'rxnetty-contexts'
                exclude group: 'io.reactivex', module: 'rxnetty-servo'
            }
    contractTestImplementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.10.2'
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir('src/integrationTest/java')
        }
        resources {
            srcDir('src/integrationTest/resources')
        }
    }
    contractTest {
        java {
            compileClasspath += main.output
            runtimeClasspath += main.output
            srcDir file('src/contractTest/java')
        }
        resources.srcDir file('src/contractTest/resources')
    }
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudBomVersion}"
    }
    dependencies {
        dependency group: 'commons-beanutils', name: 'commons-beanutils', version: versions.commonsBeanUtils


        //CVE-2022-25857
        dependencySet(group: 'org.yaml', version: '1.33') {
            entry 'snakeyaml'
        }

        // solves CVE-2014-3488, CVE-2015-2156, CVE-2019-16869
        dependencySet(group: 'io.netty', version: '4.1.87.Final') {
            entry 'netty-buffer'
            entry 'netty-codec'
            entry 'netty-codec-http'
            entry 'netty-common'
            entry 'netty-handler'
            entry 'netty-resolver'
            entry 'netty-transport'
        }

        //CVE-2021-44228, CVE-2021-44832, CVE-2021-45046, CVE-2021-45105
        dependencySet(group: 'org.apache.logging.log4j', version: '2.19.0') {
            entry 'log4j-api'
            entry 'log4j-to-slf4j'
        }

        //CVE-2022-22965
        dependencySet(group: 'org.springframework', version: '5.3.29') {
            entry 'spring-aop'
            entry 'spring-beans'
            entry 'spring-context'
            entry 'spring-context-support'
            entry 'spring-core'
            entry 'spring-expression'
            entry 'spring-jcl'
            entry 'spring-web'
            entry 'spring-webmvc'
        }

        dependencySet(
            group: 'com.fasterxml.jackson.core',
            version: versions.jackson
        ) {
            entry 'jackson-core'
            entry 'jackson-databind'
        }
        dependency group: 'org.glassfish', name: 'jakarta.el', version: '3.0.4'
    }
}

dependencies {
    integrationTestAnnotationProcessor group: 'org.projectlombok', name: 'lombok', version: versions.lombok
    integrationTestImplementation(sourceSets.test.output)

    contractTestImplementation group: 'org.apache.httpcomponents', name: 'httpclient', version: versions.httpComponents
    contractTestImplementation group: 'org.apache.httpcomponents', name: 'fluent-hc', version: versions.httpComponents

    contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'junit5', version: versions.pact_version
    contractTestImplementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson
    contractTestImplementation group: 'au.com.dius.pact.consumer', name: 'java8', version: versions.pact_version
    contractTestRuntimeOnly group: 'au.com.dius.pact.consumer', name: 'junit5', version: versions.pact_version
    contractTestRuntimeOnly group: 'au.com.dius.pact.consumer', name: 'java8', version: versions.pact_version
    contractTestRuntimeOnly group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: versions.jackson
    contractTestImplementation group: 'org.springframework.boot', name: 'spring-boot-starter-test'
    contractTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit5
    contractTestRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: versions.junit5
    contractTestImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: versions.junit5
    contractTestRuntimeOnly "org.junit.platform:junit-platform-commons:1.7.2"


}

task smoke(type: Test, description: 'Runs the smoke tests.', group: 'Verification') {
    useJUnit {
        includeCategories 'uk.gov.hmcts.reform.divorce.casemaintenanceservice.category.SmokeTest'
    }
}

task bootRunAat(type: BootRun, description: 'Runs the app using AAT config', dependsOn: 'classes', group: 'Application') {
    main = bootJar.mainClass
    doFirst() {
        classpath = sourceSets.main.runtimeClasspath
        systemProperties = System.properties
        systemProperty 'spring.profiles.active', 'aat'
    }
}

task functional(type: Test, description: 'Runs the functional tests.', group: 'Verification') {
    doFirst(){
        printf ("Available processors:::::::::: " + Runtime.runtime.availableProcessors().intdiv(2).toString())
    }
    maxParallelForks = Runtime.runtime.availableProcessors().intdiv(2) ?: 1
    forkEvery = 10
    setTestClassesDirs(sourceSets.integrationTest.output.classesDirs)
    setClasspath(sourceSets.integrationTest.runtimeClasspath)

    useJUnit()

    finalizedBy aggregate
}

task contract(type: Test) {
    group = 'Delivery pipeline'
    description = 'Runs the Consumer Pact tests'
    useJUnitPlatform()
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath
    include "uk/gov/hmcts/reform/divorce/casemaintenanceservice/client/**"
    systemProperty 'pact.rootDir', "pacts"
    environment("APPINSIGHTS_INSTRUMENTATIONKEY", "test-key")
}

pitest {
    targetClasses = ['uk.gov.hmcts.reform.divorce.casemaintenanceservice.controller.*',
                     'uk.gov.hmcts.reform.divorce.casemaintenanceservice.controller.support.*',
                     'uk.gov.hmcts.reform.divorce.casemaintenanceservice.draftstore.factory.*',
                     'uk.gov.hmcts.reform.divorce.casemaintenanceservice.event.ccd.submission.*',
                     'uk.gov.hmcts.reform.divorce.casemaintenanceservice.event.config',
                     'uk.gov.hmcts.reform.divorce.casemaintenanceservice.management.monitoring.health.*',
                     'uk.gov.hmcts.reform.divorce.casemaintenanceservice.service.*',
                     'uk.gov.hmcts.reform.divorce.casemaintenanceservice.util.*']
    excludedClasses = [ ]
    threads = 10
    outputFormats = ['XML', 'HTML']
    timestampedReports = false
    mutationThreshold = 80
}

def sonarExclusions = [
    '**uk/gov/hmcts/reform/divorce/casemaintenanceservice/domain/**/*',
    '**uk/gov/hmcts/reform/divorce/casemaintenanceservice/**/model/**/*',
    '**uk/gov/hmcts/reform/divorce/casemaintenanceservice/**/config/**/*',
    '**uk/gov/hmcts/reform/divorce/casemaintenanceservice/CaseMaintenanceServiceApplication.java',
    '**uk/gov/hmcts/reform/divorce/casemaintenanceservice/client/DraftStoreClient.java'
]

sonarqube {
    properties {
        property "sonar.projectKey", "DivorceCaseMaintenanceService"
        property "sonar.projectName", "Divorce :: case-maintenance-service"
        property "sonar.exclusions", sonarExclusions.join(", ")
        property "sonar.coverage.jacoco.xmlReportPaths", "${jacocoTestReport.reports.xml.destination.path}"
        property "sonar.pitest.mode", "reuseReport"
        property "sonar.pitest.reportsDirectory", "build/reports/pitest"
    }
}

task developAddRelaseSuffix() {
    version = "${version}-SNAPSHOT"
}

jacocoTestReport {
    executionData(test)

    reports {
        xml.enabled = true
        html.enabled = true
        xml.destination file("${buildDir}/reports/jacoco/test/jacocoTestReport.xml")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            limit {
                minimum = 0.9
            }
        }
    }
}

project.tasks['sonarqube'].dependsOn jacocoTestReport

def debug = System.getProperty("debug")
run {
    if (debug == 'true') {
        jvmArgs = ['-agentlib:jdwp=transport=dt_socket,server=y,suspend=y,address=5010']
    }
}

project.ext {
    pacticipant = 'divorce_caseMaintenanceService'
    pacticipantVersion = getCheckedOutGitCommitHash()
}

task runAndPublishConsumerPactTests(type: Test){
    logger.lifecycle("Runs pact Tests")
    testClassesDirs = sourceSets.contractTest.output.classesDirs
    classpath = sourceSets.contractTest.runtimeClasspath

}

runAndPublishConsumerPactTests.dependsOn contract

runAndPublishConsumerPactTests.finalizedBy pactPublish

def getCheckedOutGitCommitHash() {
    'git rev-parse --verify --short HEAD'.execute().text.trim()
}

pact {
    broker {
        pactBrokerUrl = System.getenv("PACT_BROKER_FULL_URL") ?: 'http://localhost:80'
    }
    publish {
        pactDirectory = 'pacts'
        tags = [System.getenv("PACT_BRANCH_NAME") ?:'Dev']
        version = project.pacticipantVersion
    }
}


task fortifyScan(type: JavaExec)  {
    main = "uk.gov.hmcts.fortifyclient.FortifyClientMainApp"
    classpath += sourceSets.test.runtimeClasspath
    jvmArgs = ['--add-opens=java.base/java.lang.reflect=ALL-UNNAMED']
}

tasks.withType(Copy) {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}
